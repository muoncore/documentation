
plugins {
  id 'com.github.johnrengelman.shadow' version '2.0.1'
}

description = 'Core Muon for Java'
dependencies {
  shadow "org.reactivestreams:reactive-streams:$reactiveStreamsVersion"

  shadow 'com.google.code.gson:gson:2.3.1'
  shadow 'org.apache.avro:avro:1.8.1'
  compile "io.projectreactor:reactor-stream:$reactorVersion"
  shadow 'org.slf4j:slf4j-api:1.7.21'

  testCompile 'org.slf4j:slf4j-simple:1.7.12'
  testCompile 'com.google.code.gson:gson:2.3.1'
  testCompile 'org.apache.avro:avro:1.8.1'
}

shadowJar {
  dependencies {
    exclude(dependency({
      !(it.moduleGroup in ["io.projectreactor"])
    }))
  }
  relocate 'reactor', 'io.muoncore.liblib.reactor'
  archiveName = "${baseName}-${version}.${extension}"
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}
task packageJavadoc(type: Jar) {
  from javadoc
  classifier = 'javadoc'
}


publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact sourceJar {
        classifier "sources"
      }
      artifact packageJavadoc
    }

    shadow(MavenPublication) { publication ->
      project.shadow.component(publication)
      pom.withXml {
        asNode().appendNode('name', "Muon Core")
        asNode().appendNode('description', 'Muon is a toolkit for building highly portable, polyglot, reactive microservices. Muon Core is the central foundation library for Muon on the JVM.')
        asNode().appendNode('url', 'http://muoncore.io')
        def license = asNode().appendNode("licenses").appendNode("license")
        license.appendNode("name", "The GNU Lesser General Public License, Version 3.0")
        license.appendNode("url", "http://www.gnu.org/licenses/lgpl-3.0.txt")
        license.appendNode("distribution", "repo")

        def developers = asNode().appendNode("developers")
        def dev = developers.appendNode("developer")
        dev.appendNode("name", "Muon Developers")
        dev.appendNode("email", "info@muoncore.io")
        dev.appendNode("organization", "Muon Project")
        dev.appendNode("organizationUrl", "http://muoncore.io")

        asNode().appendNode("scm").appendNode("url", "https://github.com/muoncore/muon-java")
        asNode().packaging[0].value = 'jar'
        asNode().dependencies.'*'.each{
          it.scope[0].value="compile"
        }
      }
    }
  }
}

install {
  repositories.mavenInstaller {
    pom.withXml {
      asNode().dependencies.'*'.each{
        it.scope[0].value="compile"
      }
    }
  }
}

task copyPomLocal(type: Copy) {
  from 'build/publications/shadow'
  into 'build/poms'
}

task copyPom(type: Copy) {
  from 'build/publications/shadow'
  into 'build/publications/mavenJava'
}

tasks.jar.enabled = false
project.tasks.artifactoryPublish.dependsOn project.tasks.shadowJar
project.tasks.artifactoryPublish.dependsOn project.tasks.shadowJar
project.tasks.artifactoryPublish.dependsOn "publishShadowPublicationToMavenLocal"
project.tasks.artifactoryPublish.dependsOn copyPom
project.tasks.install.dependsOn copyPomLocal
project.tasks.copyPomLocal.dependsOn "generatePomFileForShadowPublication"
project.tasks.copyPom.dependsOn "generatePomFileForShadowPublication"
project.tasks.copyPom.dependsOn "generatePomFileForMavenJavaPublication"

